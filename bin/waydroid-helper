#!/usr/bin/zsh

setopt ERR_EXIT PIPE_FAIL

whence -p waydroid 1>/dev/null

typeset waydroid_storage=""

typeset -A location_map=(
  "${HOME}/Documents" "${HOME}/.local/share/waydroid/data/media/0/Documents"
  "${HOME}/Downloads" "${HOME}/.local/share/waydroid/data/media/0/Download"
  "${HOME}/Music" "${HOME}/.local/share/waydroid/data/media/0/Music"
  "${HOME}/Pictures" "${HOME}/.local/share/waydroid/data/media/0/Pictures"
  "${HOME}/Videos" "${HOME}/.local/share/waydroid/data/media/0/Movies"
)

# 取消挂载函数
function _waydroid-umount() {
  for it (${(v)location_map}) {
    # 防止有多个重复挂载项
    while { mount | grep -q "$it" } {
      sudo umount -v "$it"
    }
  }
}

# 挂载函数
function _waydroid-mount() {
  # 等待系统启动完成
  while [[ "$(waydroid prop get sys.boot_completed 2>/dev/null)" != "1" ]] {
    sleep 1
  }

  for it (${(k)location_map}) {
    sudo mount -v -B -m "$it" "${location_map[${it}]}"
  }
}

function _waydroid-daemon() {
  _waydroid-mount &
  waydroid session start || true
  _waydroid-umount
}

# 根据参数执行相应操作
case "$1" in
  -u|--umount)
    _waydroid-umount
    ;;
  -m|--mount)
    _waydroid-mount
    ;;
  -d|--daemon)
    if [[ -n "$TMUX" && "$(tmux display -p '#S')" == "waydroid" ]] {
      _waydroid-daemon
    } else {
      tmux has-session -t "waydroid" 2>/dev/null || tmux new-session -d -c "$HOME" -s "waydroid" "zsh $(printf "%q" "${0:A}") --daemon"
    }
    ;;
  *)
    echo "用法: waydroid-helper [-u|--umount] [-m|--mount] [-d|--daemon]"
    echo "  -u, --umount  仅取消挂载"
    echo "  -m, --mount   仅挂载目录"
    echo "  -d, --daemon  守护模式（在tmux中运行）"
    return 1
    ;;
esac
